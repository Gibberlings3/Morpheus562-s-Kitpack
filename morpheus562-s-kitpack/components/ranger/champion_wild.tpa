// Champion of the Wild Kit 

ADD_KIT ~MO_CHAMPIONWILD~
  ~MO_CHAMPIONWILD 1 1 1 1 1 1 1 1~
  ~MO_CHAMPIONWILD 0 0 0 0 0 0 0 0 2 2 2 0 0 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~MO_CHAMPIONWILD 0 0 12 0 0 0~ // Minimum Ability - abclasrq
  ~MO_CHAMPIONWILD -1 0 0 0 0 0~ // Kit Ability Modifiers - abclsmod
  ~MO_CHAMPIONWILD 0 0 17 0 0 0~ // Requirement to Dual Class (to) - abdcedsrq
  ~MO_CHAMPIONWILD 0 0 15 0 0 0~ // Requirements to Dual Class (from) - abdcscrq
  ~MO_CHAMPIONWILD 1 0 0 1 0 0 1 0 0~ // Alignment
  ~MO_CHAMPIONWILD 0 0 0 0 0 0~ // Dual Class Options
  ~morpheus562-s-kitpack/2da/mo_chw01.2DA~
  ~K_R_H K_R_E K_R_HE~
  ~0x00004000 12~
  ~Ra0~
  ~LEAT14 * HELM07 BAG24 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~
  SAY @5542
  SAY @5543
  SAY @5544

LAF fl#add_kit_ee STR_VAR kit_name = MO_CHAMPIONWILD END

OUTER_SET championwild_code = (0x4000 + %MO_CHAMPIONWILD%)

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  SPRINT weapon_name ~%SOURCE_FILE%~
  READ_SHORT 0x1c type
  READ_ASCII 0x22 appearance (2)

  // Armor Restriction
  PATCH_IF (type = 2) AND ((~%appearance%~ STRING_COMPARE_CASE ~4a~ = 0) OR (~%appearance%~ STRING_COMPARE_CASE ~3a~ = 0)) BEGIN 
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %championwild_code% parameter2 = 9 power = 0 timing = 2 END
  END
  BUT_ONLY

COPY ~morpheus562-s-kitpack/2da/mo_chw01.2DA~              ~override~ // kit
COPY ~morpheus562-s-kitpack/spl/champion_wild/mochw01.spl~ ~override~ // Superior Defense
COPY ~morpheus562-s-kitpack/spl/champion_wild/mochwc2.spl~ ~override~ // Elegant Strike
COPY ~morpheus562-s-kitpack/eff/champion_wild/mochw02.eff~ ~override~ // Elegant Strike
COPY ~morpheus562-s-kitpack/spl/champion_wild/mochw02.spl~ ~override~ // Elegant Strike
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex01.spl~ ~override~ // Dex Bonus - 16
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex02.spl~ ~override~ // Dex Bonus - 17
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex03.spl~ ~override~ // Dex Bonus - 19
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex04.spl~ ~override~ // Dex Bonus - 21
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex05.spl~ ~override~ // Dex Bonus - 24
COPY ~morpheus562-s-kitpack/spl/champion_wild/modex06.spl~ ~override~ // Dex Bonus - 25

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Wrath of the Wild~) STR_VAR bam_file = SPPR704C RET MOCHW3A = icon END
COPY ~morpheus562-s-kitpack/spl/champion_wild/mochw03.spl~ ~override~ // Wrath of the Wild
  SAY NAME1 @5545
  SAY UNIDENTIFIED_DESC @5546
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = ~MOCHW3A~ END