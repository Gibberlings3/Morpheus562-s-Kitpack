// Battle Master Kit 

ADD_KIT ~MO_BATTLERAGER~
  ~MO_BATTLERAGER 1 1 1 1 1 1 1 1~
  ~MO_BATTLERAGER 0 0 0 0 0 0 0 0 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 0 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~MO_BATTLERAGER 9 0 0 0 0 0~ // Minimum Ability - abclasrq
  ~MO_BATTLERAGER 0 0 0 0 0 0~ // Kit Ability Modifiers - abclsmod
  ~MO_BATTLERAGER 0 0 17 0 0 0~ // Requirement to Dual Class (to) - abdcedsrq
  ~MO_BATTLERAGER 0 0 15 0 0 0~ // Requirements to Dual Class (from) - abdcscrq
  ~MO_BATTLERAGER 0 1 1 0 1 1 0 1 1~ // Alignment
  ~MO_BATTLERAGER 0 0 0 0 0 0~ // Dual Class Options
  ~morpheus562-s-kitpack/2da/mo_btr01.2DA~
  ~K_F_D~
  ~0x00004000 2~
  ~Fi0~
  ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  SAY @5594
  SAY @5595
  SAY @5596

LAF fl#add_kit_ee STR_VAR kit_name = mo_battlerager END

OUTER_SET battlerager_code = (0x4000 + %MO_BATTLERAGER%)

///////////////////////////////////////////////////////////////////////////
// Update Items to Prevent Ranged Weapon Use                             //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  SPRINT weapon_name ~%SOURCE_FILE%~
  READ_SHORT 0x31 prof ELSE 0
  READ_SHORT 0x1c type
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  //SET ranged = 0

  // Restrict Ranged Weapons and Shields
  PATCH_IF (prof = 104) OR  // Longbow
        (prof = 105) OR     // Shortbow
        (prof = 103) OR     // Crossbow
        (prof = 106) OR     // Dart
        (prof = 107) BEGIN  // Sling
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %battlerager_code% parameter2 = 9 power = 0 timing = 2 END
  END ELSE BEGIN
    // Thanks to Cam for providing the below FOR loop to check for ranged weapons header.
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      READ_SHORT (abil_off +        (index * 0x38)) type
      PATCH_IF (type = 2) BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %battlerager_code% parameter2 = 9 power = 0 timing = 2 END
        SET index = abil_num // kills loop, prevents patching if ranged item
      END
    END
  END
  BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Import Files                                                          //
///////////////////////////////////////////////////////////////////////////
COPY ~morpheus562-s-kitpack/2da/mo_btr01.2DA~              ~override~ // kit

COPY ~morpheus562-s-kitpack/spl/battlemaster/mobtr01.spl~  ~override~ // Reckless Abandon
  SAY NAME1 @5597
  SAY UNIDENTIFIED_DESC @5598
COPY ~morpheus562-s-kitpack/spl/battlemaster/mobtr01a.spl~ ~override~ // Winded
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (@5541) STR_VAR END
COPY ~morpheus562-s-kitpack/spl/battlemaster/mobtr02.spl~  ~override~ // Spiked Armor
COPY ~morpheus562-s-kitpack/spl/battlemaster/mobtr22.spl~  ~override~ // Spiked Armor Damage
  PATCH_FOR_EACH fireshield IN
          balshld2
          bdbarbde
          bdsha12a
          bdshld02
          keldorn
          leat23a
          spcl237d
          spimixd
          sppr730d
          sppr951d
          sppr952d
          spwi403d
          spwi418d
          solak1
        BEGIN
          PATCH_IF FILE_EXISTS_IN_GAME ~%fireshield%.SPL~ THEN BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 timing = 10 resist_dispel = 0 duration = 2 probability1 = 100 STR_VAR resource = ~%fireshield%~ END
          END
        END

///////////////////////////////////////////////////////////////////////////
// Add Kit to Korgan                                                     //
///////////////////////////////////////////////////////////////////////////
ACTION_IF korgan_rager THEN BEGIN
  ACTION_FOR_EACH creature IN korgan11 korgan12 korgan15 korgan8 korgan9 ohb2korg BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN
      COPY_EXISTING ~%creature%.cre~ ~override~
        WRITE_SHORT 0x246 0x4000 + ~%MO_BATTLERAGER%~
    END
  END
END