// Champion of the Silver Flame

ADD_KIT ~MO_CHAMPIONSILVER~
  ~MO_CHAMPIONSILVER 1 1 1 1 1 1 1 1~
  ~MO_CHAMPIONSILVER 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~MO_CHAMPIONSILVER 12 0 9 0 13 17~ // Minimum Ability
  ~MO_CHAMPIONSILVER 0 0 0 0 0 0~ // Kit Ability Modifiers
  ~MO_CHAMPIONSILVER 0 0 17 0 0 0~ // Requirement to Dual Class (to)
  ~MO_CHAMPIONSILVER 0 0 15 0 0 0~ // Requirements to Dual Class (from)
  ~MO_CHAMPIONSILVER 1 0 0 0 0 0 0 0 0~ // Alignment
  ~MO_CHAMPIONSILVER 0 0 0 0 0 0~ // Dual Class Options
  ~morpheus562-s-kitpack/2da/mo_chs01.2DA~
  ~K_P_H~
  ~0x00004000 6~
  ~Pa0~
  ~CHAN09 * HELM07 BAG26 RING06 RING31 * BOOT01 AMUL20 BRAC10 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  SAY @5554
  SAY @5555
  SAY @5556

LAF fl#add_kit_ee STR_VAR kit_name = MO_CHAMPIONSILVER END

OUTER_SET championsilver_code = (0x4000 + %MO_CHAMPIONSILVER%)

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  SPRINT weapon_name ~%SOURCE_FILE%~
  READ_SHORT 0x31 prof ELSE 0
  READ_SHORT 0x1c type
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  //SET ranged = 0

  // Restrict Ranged Weapons and Shields
  PATCH_IF (prof = 104) OR  // Longbow
        (prof = 105) OR     // Shortbow
        (prof = 103) OR     // Crossbow
        (prof = 106) OR     // Dart
        (prof = 107) OR     // Sling
        (type = 12) BEGIN   // Shields
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %championsilver_code% parameter2 = 9 power = 0 timing = 2 END
  END ELSE BEGIN
    // Thanks to Cam for providing the below FOR loop to check for ranged weapons header.
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      READ_SHORT (abil_off +        (index * 0x38)) type
      PATCH_IF (type = 2) BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 parameter1 = %championsilver_code% parameter2 = 9 power = 0 timing = 2 END
        SET index = abil_num // kills loop, prevents patching if ranged item
      END
    END
  END
  BUT_ONLY

COPY ~morpheus562-s-kitpack/2da/mo_chs01.2DA~                ~override~ // kit
COPY ~morpheus562-s-kitpack/spl/champion_silver/mochs01.spl~ ~override~ // Blessing of the Champion
