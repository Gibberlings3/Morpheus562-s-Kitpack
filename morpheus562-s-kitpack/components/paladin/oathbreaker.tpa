// Oathbreaker Kit 

ADD_KIT ~MO_OATHBREAKER~
  ~MO_OATHBREAKER 0 0 0 0 0 0 0 0~
  ~MO_OATHBREAKER 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~MO_OATHBREAKER 12 0 9 0 13 17~ // Minimum Ability - abclasrq
  ~MO_OATHBREAKER 0 0 0 0 0 0~ // Kit Ability Modifiers - abclsmod
  ~MO_OATHBREAKER 0 0 17 0 0 0~ // Requirement to Dual Class (to) - abdcedsrq
  ~MO_OATHBREAKER 0 0 15 0 0 0~ // Requirements to Dual Class (from) - abdcscrq
  ~MO_OATHBREAKER 0 0 1 0 0 1 0 0 1~ // Alignment
  ~MO_OATHBREAKER 0 0 0 0 0 0~ // Dual Class Options
  ~morpheus562-s-kitpack/2da/mo_oat01.2DA~
  ~~
  ~0x00004000 6~
  ~Pa0~
  ~CHAN09 * HELM07 BAG26 RING06 RING31 * BOOT01 AMUL20 BRAC10 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  SAY @5613
  SAY @5614
  SAY @5615

LAF fl#add_kit_ee STR_VAR kit_name = MO_OATHBREAKER END

APPEND ~FALLEN.2da~ ~MO_OATHBREAKER 0~

OUTER_SET oathbreaker_code = (0x4000 + %MO_OATHBREAKER%)

///////////////////////////////////////////////////////////////////////////
// Import                                                                //
///////////////////////////////////////////////////////////////////////////
COPY ~morpheus562-s-kitpack/2da/mo_oat01.2DA~            ~override~ // kit

COPY ~morpheus562-s-kitpack/spl/oathbreaker/mooat01.spl~ ~override~ // Opcode 177
COPY ~morpheus562-s-kitpack/eff/oathbreaker/mooat01.eff~ ~override~ // Set AI Script

///////////////////////////////////////////////////////////////////////////
// Update Paladin Kit                                                    //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABPA01.2DA~ override
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = AP_MOOAT01 END
    PRETTY_PRINT_2DA
  BUT_ONLY

COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~pal_kitlist~ 10
  FOR (row = 3; row < pal_kitlist; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~pal_kitlist~ row 8 kit_class
  
  PATCH_IF (%kit_class% = 6) BEGIN
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ BEGIN
      INNER_ACTION BEGIN
      COPY_EXISTING ~%kit_clab%.2DA~ override
      PATCH_IF !("%kit_label%" STRING_EQUAL_CASE "Blackguard") AND !("%kit_label%" STRING_EQUAL_CASE "MO_REAVER") BEGIN
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = AP_MOOAT01 END
        PRETTY_PRINT_2DA
      END 
      BUT_ONLY
      END
    END
  END
  END

///////////////////////////////////////////////////////////////////////////
// Fighter Stronghold                                                    //
///////////////////////////////////////////////////////////////////////////
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~morpheus562-s-kitpack/dlg/naliao.d~ 
END